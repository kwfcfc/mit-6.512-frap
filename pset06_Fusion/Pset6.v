(** * 6.512 Formal Reasoning About Programs, Fall 2025 - Pset 6 *)
Require Import Frap.Frap Pset6.Pset6Sig.
Import Swap.
(*|
Modern compilers achieve excellent performance by leveraging a wide variety of
program transformations.  For example, GCC (the GNU C compiler) version 10.2
produces the *exact same* assembly code for the following two programs (if you
want to see for yourself, try it on https://godbolt.org!):

1. Recursive version with accumulator, naive division and modulo, no function
   inlining, multiple returns, redundant ``+ 1``, not tail-recursive::

      static unsigned int affine(unsigned int n,
                                 unsigned int slope,
                                 unsigned int offset) {
          return n * slope + offset;
      }

      unsigned int collatz1(unsigned int start) {
          if (start == 1)
            return 0;
          else if (start % 2 == 0)
            return collatz1(start / 2) + 1;
          else
            return collatz1(affine(start, 3, 1)) + 1;
      }

2. Single loop, inlined function, bitwise arithmetic::

      unsigned int collatz2(unsigned int start) {
          unsigned int flight = 0;
          while (start != 1) {
              flight++;
              if ((start & 1) == 0) {
                  start = start >> 1;
              } else {
                  start = start * 2 + start + 1;
              }
          }
          return flight;
      }

The way GCC achieves this is by applying a sequence of transformation passes on
the code: you can see the output of each intermediate pass (all ~320 of them)
using ``gcc -O3 -fdump-tree-all -fdump-rtl-all -c collatz1.c``.  For instance,
the ``ssa`` pass puts the code into a form similar to the three-address code
that we saw in class, the ``tailr1`` passes convert the recursive function to a
loop, etc.

Students interested in an introduction to the magic of modern compiler
optimizations might enjoy Matt Godbolt's 2017 talk at CPPCon, *What Has My
Compiler Done for Me Lately? Unbolting the Compiler's Lid*
(https://www.youtube.com/watch?v=bSkpMdDe4g4).

In this lab, we'll see how formal methods can help us reason about the
correctness of these optimization passes, focusing on a couple of particular
optimizations.

Pset6Sig.v contains the number of points you get for each definition and
proof. Note that since we ask you to come up with some definitions
yourself, all proofs being accepted by Rocq does not necessarily guarantee a
full score: you also need to make sure that your definitions correspond to
what we ask for in the instructions. That said, if the required tests pass
and your definitions do not intentionally special-case them, you should be
fine.

*A few notes of caution*:

- Some of the proofs in this lab can be a bit long to complete fully manually:
  we encourage you to try your hand at simple automation.  However, make sure
  that your whole file compiles in a reasonable amount of time (at most a minute
  or two).

- When decomposed into the right sequence of lemmas, most of the theorems in
  this pset have relatively short proofs.  The best way to find these lemmas is
  to approach each problem cautiously, trying to work an understanding of the
  proof before diving into long series of `invert`, `econstructor`, etc.  In
  general, it's also a good idea to admit lemmas until you are sure that they
  allow you to prove the complete theorem, then go back and prove the lemmas —
  but do take the time to convince yourself that your lemmas make sense, so that
  you don't waste time using incorrect lemmas.

- We have included plenty of hints below in the HINTS section of the
  signature file.
|*)

Module Impl.

(*
  We'll be working with a small stack-based language in this lab, where we
  interpret a program as a transformation from an input stack to an output stack,
  primarily done by pushing and popping values on and off the stack.
 *)

(*
  Our values consist of natural numbers and lists of values.
  So that we can have a single value type, we use the following datatype:
 *)
Inductive stack_val :=
| val_lit (n : nat)
| val_nil
| val_cons (v1 v2 : stack_val).

(*
  However, this inductive definition admits expressions that do not conform
  to our English definition of our set of values. For example, the following
  term has the Rocq type `stack_val`: `val_cons (val_lit 0) (val_lit 1)`, even
  though we see it as ill-formed since the tail of a cons should be a list.
  In order to describe the set of well-formed values, we define the following
  datatype of stack-language type signatures and an associated typing judgment
  for stack values.

  The typing judgments in this lab are fairly straightforward since they only
  have to be concerned with natural numbers and lists, but you can think of them
  as a preview of the sort of problems that will be in next week's assignment.
 *)
Inductive ty : Set :=
| ty_nat
| ty_list (t : ty).

Inductive val_well_typed : stack_val -> ty -> Prop :=
| val_lit_wt n : val_well_typed (val_lit n) ty_nat
| val_nil_wt t : val_well_typed val_nil (ty_list t)
| val_cons_wt t v1 v2
  : val_well_typed v1 t ->
      val_well_typed v2 (ty_list t) ->
      val_well_typed (val_cons v1 v2) (ty_list t).
Local Hint Constructors val_well_typed : core.

(*
  Since a stack is a list of values, we can use this judgment to define
  a typing judgment for stacks as well. The type of a stack is just a list
  of the types of its values. Since `val_well_typed` is a binary relation,
  we can use `Forall2` from the standard library to lift it to operate on stacks.
  You can see the definition of `Forall2` by printing it:
 *)
Print Forall2.

(*
  We define `stack_well_typed` as a notation instead of a definition for some
  convenience in tactics. You don't need to pay attention to the difference
  except to know that you can't unfold `stack_well_typed`, but Rocq automatically
  will see it as a use of `Forall2`.
 *)
Notation stack_well_typed := (Forall2 val_well_typed).
Local Hint Constructors Forall2 : core.


(* Here are some definitions that we will use in the interpreter.
   Many of them have dummy cases that we do not expect to hit.
   Specifically, the benefit of all of the typing judgments is that
   they guarantee these cases will never happen.
 *)

Definition stack_unop f (s : list stack_val) :=
  match s with
  | a::s' => (f a)::s'
  | _ => (*this case never happens on well-typed programs*) s
  end.

Definition stack_binop f (s : list stack_val) :=
  match s with
  | a::b::s' => (f a b)::s'
  | _ => (*this case never happens on well-typed programs*) s
  end.

Definition stack_pop (s : list stack_val) :=
  match s with
  | a::s => (a,s)
  | _ => (*this case never happens on well-typed programs*) (val_lit 0, [])
  end.

Definition stack_peek (s : list stack_val) :=
  match s with
  | a::_ => a
  | _ => (*this case never happens on well-typed programs*) val_lit 0
  end.

Fixpoint val_app v1 v2 :=
  match v1 with
  | val_nil => v2
  | val_cons v11 v12 => val_cons v11 (val_app v12 v2)
  | val_lit _ => (*this case never happens on well-typed programs*) val_lit 0
  end.

Fixpoint val_flatmap (f : stack_val -> stack_val) v :=
  match v with
  | val_nil => val_nil
  | val_cons v1 v2 =>
      val_app (f v1) (val_flatmap f v2)
  | val_lit _ => val_lit 0
  end.

Fixpoint val_reduce (f : stack_val -> stack_val -> stack_val) vl vacc :=
  match vl with
  | val_nil => vacc
  | val_cons v1 v2 =>
      val_reduce f v2 (f vacc v1)
  | val_lit _ => val_lit 0
  end.


(*
  You will have to prove some lemmas about most of these functions to finish
  the later exercises. We've given you one of the more complicated ones here
  to prove, but you should come up with your own for the other functions as
  needed.
 *)
Lemma val_flatmap_sound t1 t2 f l
  : (forall x, val_well_typed x t1 -> val_well_typed (f x) (ty_list t2)) ->
    val_well_typed l (ty_list t1) ->
    val_well_typed (val_flatmap f l) (ty_list t2).
Proof.
  intros Hx Hl. induction l;
  [ inversion Hl |              (* val_lit is not possible for ty_list  *)
    simpl; apply val_nil_wt |   (* val_nil is easy to prove *)
    ].
  simpl. invert Hl. pose proof (IHl2 H3) as Hfl2.
  pose proof (Hx l1 H2) as Hfl1. induction (f l1); simpl.
  1, 2: simpl in Hfl1.
  - inversion Hfl1.        (* val_lit is not well typed. *)
  - exact Hfl2.            (* val_nil gives exactly Hfl2. *)
  - invert Hfl1. apply IHs2 in H5. now apply val_cons_wt.
Qed.

Lemma val_reduce_sound t1 t2 f l
  : (forall x acc', val_well_typed x t1 ->
               val_well_typed acc' t2 ->
               val_well_typed (f acc' x) t2) ->
    val_well_typed l (ty_list t1) ->
    (forall acc, val_well_typed acc t2 ->
            val_well_typed (val_reduce f l acc) t2).
Proof.
  intros Hf Hl acc Hacc. induct l; [ inversion Hl | simpl; exact Hacc | ].
  simpl. invert Hl.
  apply (IHl2 Hf H3 (f acc l1)).
  apply (Hf l1 acc H2 Hacc).
Qed.

Lemma val_to_single_stack_typed x t
  : val_well_typed x t <-> stack_well_typed [x] [t].
Proof.
  split.
  - intro Hv. apply Forall2_cons; [ exact Hv | apply Forall2_nil ].
  - intro Hs. invert Hs. exact H2.
Qed.

(*
  Now that we have values, we can define our syntax of commands.
  Their purposes are as follows:
  - cmd_atom: push a value onto the stack
  - cmd_unop: pop a value from the stack, perform an unary operation on it,
              and push the result back
  - cmd_binop: pop two values from the stack, perform a binary operation on them,
              and push the result back
  - cmd_swap: switch 2 values in the stack, determined by their positions.
              `n1` must always be the earlier (smaller) position.
  - cmd_flatmap: the most interesting operation in this assignment. It pops a
                 list value from the stack, runs a command `cf` on each element of
                 the list, and appends the outputs of that command in order.
  - cmd_reduce: pops a list value and another value from the stack and accumulates
                an output value by starting with the second value and running
                a command `cf` on the current accumulator and each item in the list
                in turn.
  - cmd_skip: All other commands take the rest of the program as their final arguments.
              We use `cmd_skip` when we want to end the current command.

  You may notice that we're playing a little trick here with cmd_unop and cmd_binop.
  These cases of our stack language actually take in Rocq functions directly.
  This has some drawbacks, but it allows us to use these two constructors to add
  all sorts of operations to our language, from arithmetic to cons and ranges.
 *)
Inductive stack_cmd :=
| cmd_atom (v : stack_val) (c : stack_cmd)
| cmd_unop (f : stack_val -> stack_val) (c : stack_cmd)
| cmd_binop (f : stack_val -> stack_val -> stack_val) (c : stack_cmd)
| cmd_swap (n1 n2 : nat) (c : stack_cmd)
| cmd_flatmap (cf : stack_cmd) (c : stack_cmd)
| cmd_reduce (cf : stack_cmd) (c : stack_cmd)
| cmd_skip.



(*
  This is the typing judgment for commands. You should read `cmd_well_typed S c S'`
  as "On an input stack of type S, running c must produce an output stack of type S'".

  Notice that in the flatmap and reduce cases, `cf` only works with fixed input and
  output stacks. This means that it's not allowed to affect the rest of the stack,
  for example by swapping with some earlier value.
 *)
Inductive cmd_well_typed : list ty -> stack_cmd -> list ty -> Prop :=
| cmd_atom_wt v t S c S'
  : val_well_typed v t ->
    cmd_well_typed (t::S) c S' ->
    cmd_well_typed S (cmd_atom v c) S'
| cmd_unop_wt f t1 t2 S c S'
  : (forall v, val_well_typed v t1 -> val_well_typed (f v) t2) ->
    cmd_well_typed (t2::S) c S' ->
    cmd_well_typed (t1::S) (cmd_unop f c) S'
| cmd_binop_wt f t1 t2 t3 S c S'
  : (forall v1 v2, val_well_typed v1 t1 ->
                   val_well_typed v2 t2 ->
                   val_well_typed (f v1 v2) t3) ->
    cmd_well_typed (t3::S) c S' ->
    cmd_well_typed (t1::t2::S) (cmd_binop f c) S'
| cmd_swap_wt S n1 n2 c S'
  : n1 < n2 ->
    n2 < length S ->
    cmd_well_typed (swap n1 n2 S) c S' ->
    cmd_well_typed S (cmd_swap n1 n2 c) S'
| cmd_flatmap_wt S (cf : stack_cmd) t1 t2 c S'
  : cmd_well_typed ((ty_list t2)::S) c S' ->
    cmd_well_typed [t1] cf [ty_list t2] ->
    cmd_well_typed ((ty_list t1)::S) (cmd_flatmap cf c) S'
| cmd_reduce_wt S (cf : stack_cmd) t t_acc c S'
  : cmd_well_typed (t_acc::S) c S' ->
    cmd_well_typed [t; t_acc] cf [t_acc] ->
    cmd_well_typed ((ty_list t)::t_acc::S) (cmd_reduce cf c) S'
| cmd_skip_wt S
  : cmd_well_typed S (cmd_skip) S.
Local Hint Constructors cmd_well_typed : core.


(*
  This is our interpreter, which defines the behavior of our programs.
  Since all programs in this language terminate, we can define it as a
  regular Rocq function that takes a command and a stack and runs the
  command against the stack.
 *)
Fixpoint interp_cmd (c : stack_cmd) (s : list stack_val) : list stack_val :=
  match c with
  | cmd_atom v c' => interp_cmd c' (v::s)
  | cmd_unop f c' => interp_cmd c' (stack_unop f s)
  | cmd_binop f c' => interp_cmd c' (stack_binop f s)
  | cmd_swap n1 n2 c' => interp_cmd c' (swap n1 n2 s)
  | cmd_flatmap cf c' =>
      let (l,s1) := stack_pop s in
      let out := val_flatmap (fun x => stack_peek (interp_cmd cf [x])) l in
      interp_cmd c' (out::s1)
  | cmd_reduce cf c' =>
      let (l,s) := stack_pop s in
      let (acc,s) := stack_pop s in
      let out := val_reduce (fun acc x => stack_peek (interp_cmd cf [x;acc])) l acc in
      interp_cmd c' (out::s)
  | cmd_skip => s
  end.




(*
  Now let's prove that our interpreter satisfies our typing judgment.
  In other words, that running a well-typed command on a well-typed
  input stack produces a well-typed output stack.

  HINT: If you aren't sure what to do in the `cmd_reduce` case,
  look at `val_flatmap_sound` for inspiration.
  If you're really stuck, read HINT 1 in Pset6Sig.v.
 *)
Lemma interp_sound S c S'
  : cmd_well_typed S c S' ->
    forall s, stack_well_typed s S ->
              stack_well_typed (interp_cmd c s) S'.
Proof.
  induct 1; intros s Hs; simpl.
  - apply IHcmd_well_typed. now apply Forall2_cons.
  - apply IHcmd_well_typed. induct s; [ inversion Hs | ].
    simpl. invert Hs. apply Forall2_cons; [ apply (H a H4) | exact H6 ].
  - apply IHcmd_well_typed. invert Hs. induct l; [ inversion H5 | ].
    invert H5. simpl. apply Forall2_cons; [ apply (H x a H4 H6) | exact H8 ].
  - apply IHcmd_well_typed. (* Forall2_swap is a theorem for swap *)
    apply Forall2_swap. exact Hs.
  - induct s; [ inversion Hs | ]. (* disgard the inmpossible s *)
    simpl. apply IHcmd_well_typed1. invert Hs.
    apply Forall2_cons; [ | exact H6 ]. (* use val_flatmap_sound to prove *)
    apply (val_flatmap_sound t1 t2 _ a); [ | exact H4 ]. intros x Hx.
    (* convert [x] to a stack, list of values *)
    specialize (IHcmd_well_typed2 [x]). apply val_to_single_stack_typed in Hx.
    apply IHcmd_well_typed2 in Hx.
    (* destruct interp_cmd ... to assert it as a single value list *)
    destruct (interp_cmd cf [x]); [ invert Hx | ].
    destruct l; [ | invert Hx; invert H8 ].
    apply val_to_single_stack_typed in Hx. simpl. exact Hx.
  - destruct s; [ invert Hs | ]. simpl. (* Do two inversions to show s as a::b::l *)
    destruct s0; [ invert Hs; invert H6 | ]. simpl. apply IHcmd_well_typed1.
    invert Hs. invert H6. apply Forall2_cons; [ | exact H8 ].
    (* use val_reduce sound lemma to prove *)
    apply val_reduce_sound with (t1 := t); [ | exact H4 | exact H5 ].
    intros x acc Hxt Hacc.
    (* Use IHcmd_well_typed2 to prove t_acc, it has [t; t_acc]. *)
    assert (Hxacc: stack_well_typed [x; acc] [t; t_acc]) by
      (apply Forall2_cons; [ exact Hxt | ]; apply val_to_single_stack_typed;
       exact Hacc).
    apply IHcmd_well_typed2 in Hxacc.
    (* destruct interp_cmd cf [x; acc] as a::s *)
    destruct (interp_cmd cf [x; acc]); [ inversion Hxacc | ]. simpl.
    invert Hxacc. exact H6.
  - exact Hs.
Qed.


(*
  Sometimes it's useful to combine two sequences of commands.
  Define a function `cmd_seq` so that the output is the
  concatenation of its inputs and you can prove the two following
  lemmas.
 *)
Fixpoint cmd_seq (c1 c2 : stack_cmd) : stack_cmd :=
  match c1 with
  | cmd_atom v c11 => cmd_atom v (cmd_seq c11 c2)
  | cmd_unop f c11 => cmd_unop f (cmd_seq c11 c2)
  | cmd_binop f c11 => cmd_binop f (cmd_seq c11 c2)
  | cmd_swap n1 n2 c11 => cmd_swap n1 n2 (cmd_seq c11 c2)
  | cmd_flatmap cf c11 => cmd_flatmap cf (cmd_seq c11 c2)
  | cmd_reduce cf c11 => cmd_reduce cf (cmd_seq c11 c2)
  | cmd_skip => c2
  end.

Lemma cmd_seq_wt S1 S2 S3 c1 c2
  : cmd_well_typed S1 c1 S2 ->
    cmd_well_typed S2 c2 S3 ->
    cmd_well_typed S1 (cmd_seq c1 c2) S3.
Proof.
  induct 1; intro Hc2; simpl; try (apply IHcmd_well_typed in Hc2; econstructor);
    try exact H; try exact Hc2; try exact H0;
    [ apply cmd_flatmap_wt with (t2 := t2) | apply cmd_reduce_wt ];
    try apply (IHcmd_well_typed1 Hc2); try exact H0.
Qed.

Lemma interp_seq c1 c2 s
  : interp_cmd (cmd_seq c1 c2) s
    = interp_cmd c2 (interp_cmd c1 s).
Proof.
  induct c1; simpl; try apply IHc1.
  - destruct s; simpl. all: apply IHc1_2.
  - destruct s; simpl; [ apply IHc1_2 | ].
    destruct s0; simpl. all: apply IHc1_2.
  - reflexivity.
Qed.


(*
  Let's take a look at ways to optimize programs in our language.
  You may have noticed in our earlier tests that it's often convenient
  to write `stack_cmd` programs that push constants and immediately
  operate on them, or that perform a couple unops and/or binops in sequence.
  Let's take advantage of the way we defined `stack_cmd` to collapse
  those operations where possible. We'll call this "partial evaluation"
  since we're effectively interpreting portions of our command sequence
  to compute parts of the result ahead of time.

  For example, if we have a `cmd_atom` immediately followed by a
  `cmd_binop`, we want to combine the two into a `cmd_unop` by filling in
  one argument of the `cmd_binop`'s function. Take a look at the tests
  to get a better idea of this function's expected behavior.
 *)
Fixpoint partial_eval c :=
  match c with
  | cmd_atom v c' =>
      match partial_eval c' with
      | cmd_unop f c'' => cmd_atom (f v) c''
      | cmd_binop f c'' => cmd_unop (f v) c''
      | c'_fused => cmd_atom v c'_fused
      end
  | cmd_unop f c' =>
      match partial_eval c' with
      | cmd_unop g c'' => cmd_unop (fun v => g (f v)) c''
      | cmd_binop g c'' => cmd_binop (fun v1 v2 => g (f v1) v2) c''
      | c'_fused => cmd_unop f c'_fused
      end
  | cmd_binop f c' =>
      match partial_eval c' with
      | cmd_unop g c'' => cmd_binop (fun v1 v2 => g (f v1 v2)) c''
      | c'_fused => cmd_binop f c'_fused
      end
  | cmd_swap n1 n2 c' => cmd_swap n1 n2 (partial_eval c')
  | cmd_flatmap cf1 c' => cmd_flatmap (partial_eval cf1) (partial_eval c')
  | cmd_reduce cf c' => cmd_reduce (partial_eval cf) (partial_eval c')
  | cmd_skip => cmd_skip
  end.


(* Some common commands for use in our test cases *)
Definition val_add x y :=
  match x,y with
  | val_lit xl, val_lit yl => val_lit (xl + yl)
  | _,_ => (*this case never happens on well-typed programs*) val_lit 0
  end.

Definition val_square x :=
  match x with
  | val_lit xl => val_lit (Nat.square xl)
  | _ => (*this case never happens on well-typed programs*) val_lit 0
  end.

Definition cmd_singleton := cmd_unop (fun x => val_cons x val_nil).
Definition cmd_lit n := cmd_atom (val_lit n).
Definition cmd_cons := cmd_binop val_cons.
Definition cmd_add := cmd_binop val_add.

Lemma partial_eval_test1
  : partial_eval (cmd_atom (val_lit 2) (cmd_unop val_square cmd_skip))
    = (cmd_atom (val_lit 4) cmd_skip).
Proof. equality. Qed.

Lemma partial_eval_test2
  : partial_eval (cmd_atom (val_lit 5) (cmd_binop val_add cmd_skip))
    = (cmd_unop (val_add (val_lit 5)) cmd_skip).
Proof. equality. Qed.

Lemma partial_eval_test3
  : partial_eval (cmd_unop val_square (cmd_unop val_square cmd_skip))
    = (cmd_unop (fun v => val_square (val_square v)) cmd_skip).
Proof. equality. Qed.

Lemma partial_eval_test4
  : partial_eval (cmd_binop val_add (cmd_unop val_square cmd_skip))
    = (cmd_binop (fun v1 v2 => val_square (val_add v1 v2)) cmd_skip).
Proof. equality. Qed.

Lemma partial_eval_test5
  : partial_eval (cmd_swap 0 2 (cmd_atom (val_lit 2) (cmd_atom (val_lit 3) (cmd_binop val_add cmd_skip))))
    = cmd_swap 0 2 (cmd_atom (val_lit 5) cmd_skip).
Proof. equality. Qed.

Lemma partial_eval_test6
  : partial_eval (cmd_unop val_square (cmd_atom (val_lit 1) (cmd_binop val_add (cmd_unop val_square cmd_skip))))
    = cmd_unop (fun x => val_square (val_add (val_lit 1) (val_square x))) cmd_skip.
Proof. equality. Qed.

Lemma partial_eval_test7
  : partial_eval (cmd_flatmap (cmd_atom (val_lit 2) (cmd_binop val_add (cmd_singleton cmd_skip)))
               (cmd_atom (val_lit 2) (cmd_unop val_square cmd_skip)))
    = cmd_flatmap (cmd_unop (fun v2 => val_cons (val_add (val_lit 2) v2) val_nil) cmd_skip)
        (cmd_atom (val_lit 4) cmd_skip).
Proof. equality. Qed.

(* With any program transformation, we want to make sure
   that it preserves all the right invariants, the most
   basic of which is type soundness, the idea that,
   given well-formed input, our optimization produces
   well-formed output.

   Since types are the focus of the next assignment,
   we've proven this for you.
 *)
Lemma partial_eval_sound S c S'
  : cmd_well_typed S c S' ->
    cmd_well_typed S (partial_eval c) S'.
Proof.
  induct 1;
    simplify;
    eauto.
  all:cases (partial_eval c);
    simplify;
    eauto.
  all: invert IHcmd_well_typed; eauto.
Qed.


(* Lemma for fun extensionality *)
Lemma flatmap_funext_typed f g l t1
  : (forall x, val_well_typed x t1 -> f x = g x) ->
    val_well_typed l (ty_list t1) ->
    val_flatmap f l = val_flatmap g l.
Proof.
  induct 1; simpl; [ reflexivity | ].
  pose proof (H v1 H0_) as Hfg. rewrite Hfg.
  assert (Hfv2: val_flatmap f v2 = val_flatmap g v2) by
    (now apply IHval_well_typed2 with (t1 := t1)).
  destruct (g v1); simpl; [ reflexivity | exact Hfv2 | ].
  simpl. rewrite Hfv2. reflexivity.
Qed.

Lemma flatmap_funext f g l
  : (forall x, f x = g x) ->
    val_flatmap f l = val_flatmap g l.
Proof.
  induct l; intros Heq; simpl; try reflexivity.
  rewrite (Heq l1). rewrite (IHl2 Heq). reflexivity.
Qed.

Lemma reduce_funext_typed f g l t1 t2
  : (forall x acc', val_well_typed x t1 ->
               val_well_typed acc' t2 ->
               ((val_well_typed (f acc' x) t2) /\ (f acc' x = g acc' x))) ->
    val_well_typed l (ty_list t1) ->
    (forall acc, val_well_typed acc t2 ->
            val_reduce f l acc = val_reduce g l acc).
Proof.
  intros Hfg Hl acc Hacc. induct l; [ inversion Hl | simpl; reflexivity | ].
  invert Hl. simpl. pose proof (Hfg l1 acc H2 Hacc) as Hl1.
  destruct Hl1 as [Hl1 Heq]. rewrite <- Heq.
  apply IHl2 with (t1 := t1) (t2 := t2); [ exact Hfg | exact H3 | exact Hl1 ].
Qed.

(*
  Now that we've warmed up, let's get to the meat of this assigment,
  proving compiler correctness. Since we've defined the semantics of
  our language with an interpreter, we want to show that, given an
  arbitrary (well-typed) stack, interpreting the output of our compiler
  will give us the same result as interpreting the input. If you're having
  trouble, make sure to check the hints, as this proof is tricky.

  WARNING: For this assignment in particular, be careful of simplifying
           too early. `simplify` is helpful, but it doesn't always do what
           you want when dealing with the cases here. Our solution does
           use `simplify`, but you should specifically be cautious of
           using it after using `cases (partial_eval c)` (which will likely
           appear in your proof).

  If you're having trouble with showing that your stack has enough elements
  (e.g. in the binop case), read HINT 2 in Pset6Sig.v.

  If you're having trouble with the function argument to val_flatmap,
  read HINT 3 in Pset6Sig.v.

 *)
Lemma partial_eval_correct S c S'
  : cmd_well_typed S c S' ->
    forall s, stack_well_typed s S -> interp_cmd (partial_eval c) s = interp_cmd c s.
Proof.
  induct 1; intros s Hs.
  - simpl. assert (Hts: stack_well_typed (v :: s) (t :: S)) by
      (now apply Forall2_cons). cases (partial_eval c).
    all: try (simpl; apply IHcmd_well_typed in Hts; rewrite <- Hts; simpl;
              reflexivity).
    pose proof (partial_eval_sound _ c S' H0) as Hpc. rewrite Heq in Hpc.
    invert Hs. invert Hpc.    (* Hpc is well typed if s has more than one cmd *)
    apply IHcmd_well_typed in Hts. rewrite <- Hts. simpl. reflexivity.
  - simpl. invert Hs. apply H in H4.
    assert (Hts: stack_well_typed ((f x) :: l) (t2 :: S)) by
      (now apply Forall2_cons). cases (partial_eval c).
    all: try (simpl; apply IHcmd_well_typed in Hts; rewrite <- Hts; simpl;
              reflexivity).
    pose proof (partial_eval_sound _ c S' H0) as Hpc. rewrite Heq in Hpc.
    invert H5; [ invert Hpc | ]. (* l should have more than one cmd *)
    invert Hpc. apply IHcmd_well_typed in Hts. simpl. rewrite <- Hts. simpl.
    reflexivity.
  - simpl. invert Hs. invert H5. pose proof (H x x0 H4 H6) as Hbinop.
    assert (Hts: stack_well_typed ((f x x0) :: l0) (t3 :: S)) by
      (now apply Forall2_cons). cases (partial_eval c).
    all: try (simpl; apply IHcmd_well_typed in Hts; rewrite <- Hts; simpl;
              reflexivity).
  - simpl. invert Hs; [ inversion H0 | ]. apply IHcmd_well_typed.
    (* Use Forall2_swap and cons *)
    apply Forall2_swap. now apply Forall2_cons.
  - invert Hs. invert H4;
    [ simpl; apply IHcmd_well_typed1; apply Forall2_cons;
      [ apply val_nil_wt | exact H5 ]
    | ].
    simpl.
    assert (Hcf: forall x : stack_val,
               val_well_typed x t1 ->
               interp_cmd (partial_eval cf) [x] = interp_cmd cf [x]) by
      (intros x Hx; apply val_to_single_stack_typed in Hx;
        now apply IHcmd_well_typed2).
    rewrite flatmap_funext_typed
      with (t1 := t1)
           (g := (fun x : stack_val => stack_peek (interp_cmd cf [x])));
      [ | intros x Hx; apply f_equal; now apply Hcf | exact H6 ].
    rewrite Hcf; [ | exact H2]. apply IHcmd_well_typed1. apply Forall2_cons;
      [ | exact H5 ].
    apply val_to_single_stack_typed in H2.
    pose proof (interp_sound _ _ _ H0 [v1] H2) as Hcfv1.
    (* disgard (interp_cmd cf [v1]) as [] case *)
    destruct (interp_cmd cf [v1]); [ inversion Hcfv1 | ]. simpl.
    induct s.
    + simpl. rewrite Forall2_cons_iff in Hcfv1. destruct Hcfv1 as [Hvlit _].
      invert Hvlit.
    + simpl. apply val_flatmap_sound with (t1 := t1); [ | exact H6 ].
      intros x Hx. apply val_to_single_stack_typed in Hx.
      apply (interp_sound _ _ _ H0 [x]) in Hx.
      destruct (interp_cmd cf [x]); [ invert Hx | ]. simpl.
      rewrite Forall2_cons_iff in Hx. now destruct Hx as [Hv _].
    + simpl. invert Hcfv1. invert H7. apply val_cons_wt; [ exact H8 | ].
      apply IHs2 with (l0 := l0). now apply Forall2_cons.
  - invert Hs. invert H4;
      [ invert H5; simpl; apply IHcmd_well_typed1; now apply Forall2_cons | ].
    invert H5. assert (Htacc: forall x acc,
                          val_well_typed x t ->
                          val_well_typed acc t_acc ->
                          stack_well_typed [x; acc] [t; t_acc]) by
      (intros x0 acc Hx Hacc; apply Forall2_cons; [ exact Hx | now apply Forall2_cons ]).
    pose proof (IHcmd_well_typed2 _ (Htacc v1 x H2 H7)) as Hcf. simpl.
    rewrite Hcf.
    (* Add two auxiliary to prove interp_cmd cf one step is well typed *)
    rewrite reduce_funext_typed with
      (t1 := t) (t2 := t_acc)
      (g := (fun acc x0 : stack_val => stack_peek (interp_cmd cf [x0; acc]))).
    + apply IHcmd_well_typed1. apply Forall2_cons; [ | exact H8 ].
      apply val_reduce_sound with (t1 := t);
        [ intros x0 acc Hx0 Hacc | exact H6 | ].
      * apply interp_sound with (s := [x0; acc]) in H0; [ | now apply Htacc ].
        now invert H0.
      * apply interp_sound with (s := [v1; x]) in H0; [ | now apply Htacc ].
        now invert H0.
    + intros x0 acc' Hx0 Hacc'. split.
      * rewrite (IHcmd_well_typed2 _ (Htacc x0 acc' Hx0 Hacc')).
        apply interp_sound with (s := [x0; acc']) in H0; [ | now apply Htacc ].
        now invert H0.
      * apply f_equal. apply (IHcmd_well_typed2 _ (Htacc x0 acc' Hx0 Hacc')).
    + exact H6.
    + apply interp_sound with (s := [v1; x]) in H0;
        [ now invert H0 | now apply Htacc ].
  - simpl. reflexivity.
Qed.


(*
  Let's try another compiler optimization. It turns out that when we have
  two flatmap commands in a row, we can reorder them so that the second
  one operates on each output of the first as it's produced. In other
  words, instead of generating a whole intermediate list, we can compute
  the final output as we calculate each intermediate element. This idea
  is from a family of optimizations called list fusion.

  The following lemma formalizes this idea.


  If you're having trouble with the function argument to val_flatmap,
  read HINT 4 in Pset6Sig.v.
 *)
Lemma flatmap_fuse : forall cf1 cf2 c s,
    interp_cmd (cmd_flatmap cf1 (cmd_flatmap cf2 c)) s
    = interp_cmd (cmd_flatmap (cmd_seq cf1 (cmd_flatmap cf2 cmd_skip)) c) s.
Proof.
  intros cf1 cf2 c s. induct s; [ simpl; reflexivity | ]. simpl. f_equal.
  rewrite flatmap_funext with
    (f := (fun x : stack_val =>
             stack_peek
               (interp_cmd (cmd_seq cf1 (cmd_flatmap cf2 cmd_skip)) [x])))
    (g := (fun x : stack_val =>
             stack_peek
               (interp_cmd (cmd_flatmap cf2 cmd_skip) (interp_cmd cf1 [x]))));
    [ | intro x; f_equal; apply interp_seq ].
  Abort.


(*
  Now, define an optimization pass that does this transformation on an
  arbitrary input command. Try to fuse as many instances of consecutive
  flatmaps as you can. You don't have to catch everything (there is one
  specific corner case that is difficult). For full credit, you should
  pass all of the test cases below without hardcoding them. If your
  definition isn't passing all of the test cases, try to compare it to
  our `partial_eval` compiler earlier and see if you're missing out on any
  chances to optimize.

  If you're having trouble with the tests, read HINT 5 in Pset6Sig.v.
 *)
Fixpoint loop_fuse (c : stack_cmd) : stack_cmd.
Admitted.

(*
  Your loop_fuse optimizer should pass all of the following tests.
  In the event that your optimizer fuses more `cmd_flatmap`s than ours
  and this causes one or more of these tests to fail, admit the failing test,
  add a corresponding passing test, and explain why the second output is superior.
 *)

Lemma loop_fuse_test1
  : loop_fuse (cmd_flatmap (cmd_singleton (cmd_lit 0 (cmd_cons cmd_skip)))
                 (cmd_flatmap (cmd_lit 1 (cmd_add (cmd_singleton cmd_skip))) cmd_skip))
    = (cmd_flatmap (cmd_singleton
                      (cmd_lit 0
                         (cmd_cons
                            (cmd_flatmap (cmd_lit 1 (cmd_add (cmd_singleton cmd_skip)))
                               cmd_skip))))
         cmd_skip).
Proof. (* equality. *) Admitted.

Lemma loop_fuse_test2
  : loop_fuse (cmd_flatmap (cmd_flatmap (cmd_unop val_square (cmd_singleton cmd_skip))
                              (cmd_flatmap (cmd_unop val_square (cmd_singleton cmd_skip))
                                 (cmd_singleton cmd_skip)))
                 cmd_skip)
    = cmd_flatmap
         (cmd_flatmap
            (cmd_unop val_square
               (cmd_singleton
                  (cmd_flatmap (cmd_unop val_square (cmd_singleton cmd_skip))
                     cmd_skip)))
            (cmd_singleton cmd_skip))
         cmd_skip.
Proof. (* equality. *) Admitted.


Lemma loop_fuse_test3
  : loop_fuse (cmd_flatmap (cmd_unop val_square (cmd_singleton cmd_skip))
                 (cmd_flatmap (cmd_singleton (cmd_lit 0 (cmd_cons cmd_skip)))
                 (cmd_flatmap (cmd_lit 1 (cmd_add (cmd_singleton cmd_skip))) cmd_skip)))
    = cmd_flatmap
        (cmd_unop val_square
           (cmd_singleton
              (cmd_flatmap
                 (cmd_singleton
                    (cmd_atom (val_lit 0)
                       (cmd_binop val_cons
                          (cmd_flatmap
                             (cmd_atom (val_lit 1)
                                (cmd_binop val_add (cmd_singleton cmd_skip)))
                             cmd_skip))))
                 cmd_skip)))
        cmd_skip.
Proof. (* equality. *) Admitted.


(* As a first step, let's prove that this optimization preserves
   our typing judgment like before. The proof will be very similar
   to the one for `op_fuse`.
 *)
Lemma loop_fuse_sound S c S'
  : cmd_well_typed S c S' ->
    cmd_well_typed S (loop_fuse c) S'.
Proof.
Admitted.


(*
  Now for the largest proof of the pset, let's prove that `loop_fuse` is correct.
  Make sure you've attempted `op_fuse_correct` first since the proof is similar
  and relies on some of the same lemmas.
 *)
Lemma loop_fuse_correct S c S'
  : cmd_well_typed S c S' ->
    forall s, stack_well_typed s S -> interp_cmd (loop_fuse c) s = interp_cmd c s.
Proof.
Admitted.



End Impl.

Module ImplCorrect : Pset6Sig.S := Impl.

(* Authors:
  Dustin Jamner
  Paul Mure
*)
